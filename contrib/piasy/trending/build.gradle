/*
 * The MIT License (MIT)
 *
 * Copyright (c) 2017 Piasy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

apply from: "$rootProject.projectDir/buildsystem/buildings.gradle"

if (isComposeBuild) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}

apply plugin: 'com.chenenyu.router'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'net.ltgt.errorprone'
apply from: "$rootProject.projectDir/AndroidCodeQualityConfig/quality.gradle"

def isTravis = "true" == System.getenv("TRAVIS")

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.androidBuildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        versionCode rootProject.ext.releaseVersionCode
        versionName rootProject.ext.releaseVersionName

        consumerProguardFiles 'proguard-rules.pro'

        if (!isComposeBuild) {
            applicationId "com.github.piasy.octostars.features.trending"
        }

        multiDexEnabled true
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        sign {
            storeFile signStoreFile
            storePassword signStorePassword
            keyAlias signKeyAlias
            keyPassword signKeyPassword
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled isTravis
            signingConfig signingConfigs.sign
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.sign
        }
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = false
        javaMaxHeapSize "4g"
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.properties'
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.xml'
        exclude 'META-INF/rxjava.properties'
    }

    if (isTravis) {
        lintOptions {
            abortOnError false
        }
    }
}

unMock {
    downloadFrom 'https://oss.sonatype.org/content/groups/public/org/robolectric/android-all/6.0.0_r1-robolectric-0/android-all-6.0.0_r1-robolectric-0.jar'
    downloadTo "${System.getenv('HOME')}/.gradle/caches/"

    keep "android.text.TextUtils"
    keepStartingWith "org.json."
}

dependencies {
    compile project(':contrib:bridge')
    releaseProvided project(path: ':contrib:business', configuration: 'release')

    compile featureDependencies.jsoup

    provided aptDependencies.autoValue // for ide
    annotationProcessor aptDependencies.autoValue // for okbuck
    provided aptDependencies.autoGsonApt // for ide
    annotationProcessor aptDependencies.autoGsonApt // for okbuck
    annotationProcessor aptDependencies.autoParcelApt

    annotationProcessor aptDependencies.daggerApt
    annotationProcessor aptDependencies.butterKnifeApt

    retrolambdaConfig aptDependencies.retrolambda
    errorprone aptDependencies.errorprone

    debugCompile project(path: ':contrib:business', configuration: 'debug')
    debugCompile project(':mocks')

    testCompile project(':mocks')
    testCompile project(':testbase')

    androidTestCompile project(':mocks')
    androidTestCompile project(':testbase')
}
