/*
 * The MIT License (MIT)
 *
 * Copyright (c) 2016 Piasy
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
import static org.apache.tools.ant.taskdefs.condition.Os.FAMILY_WINDOWS
import static org.apache.tools.ant.taskdefs.condition.Os.isFamily

apply plugin: 'com.android.application'
apply plugin: 'com.frogermcs.androiddevmetrics'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'com.ofg.uptodate'
apply plugin: 'com.getkeepsafe.dexcount'
apply from: '../AndroidCodeQualityConfig/quality.gradle'

def keyConfigPath
if (isFamily(FAMILY_WINDOWS)) {
    keyConfigPath = System.getenv('USERPROFILE') + File.separator + ".ssh" + File.separator +
            "piasyPrivate.properties"
} else {
    keyConfigPath = System.getenv('HOME') + File.separator + ".ssh" + File.separator +
            "piasyPrivate.properties"
}

Properties props = new Properties()
if (new File(keyConfigPath).exists()) {
    props.load(new FileInputStream(file(keyConfigPath)))
}

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.androidBuildToolsVersion

    defaultConfig {
        applicationId "com.github.piasy.lg"
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        multiDexEnabled true
        testInstrumentationRunner "com.github.piasy.lg.CustomTestRunner"

        buildConfigField "boolean", "REPORT_CRASH", "true"
        buildConfigField "boolean", "INSTALL_LEAK_CANARY", "false"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isTravis
        javaMaxHeapSize "4g"
    }

    aaptOptions {
        additionalParameters "--no-version-vectors"
    }

    signingConfigs {
        develop {
            storeFile file('../buildsystem/OpenKey.jks')
            storePassword '123456'
            keyAlias '123456'
            keyPassword '123456'
        }

        production {
            storeFile file(props['keystore'])
            storePassword props['keystore.password']
            keyAlias props['key.alias']
            keyPassword props['key.password']
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.develop
        }

        release {
            minifyEnabled true
            debuggable false
            shrinkResources true
            signingConfig signingConfigs.production
            //signingConfig signingConfigs.production
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro',
                    'proguard-fresco.pro'
        }
    }

    productFlavors.whenObjectAdded { flavor ->
        def flavorData = rootProject.ext[flavor.name]
    }

    productFlavors {
        develop {
            buildConfigField "boolean", "REPORT_CRASH", "false"
            buildConfigField "boolean", "INSTALL_LEAK_CANARY", "true"
            // this maybe different with product flavor for real app dev
            buildConfigField "String", "API_BASE_URL", "\"http://7xsouz.com2.z0.glb.clouddn.com/\""
        }

        product {
            buildConfigField "String", "API_BASE_URL", "\"http://7xsouz.com2.z0.glb.clouddn.com/\""
        }
    }

    sourceSets {
        test.java.srcDirs += 'build/generated/source/apt/develop/debug'
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }
    }
}

unMock {
    // URI to download the android-all.jar from. e.g. https://oss.sonatype
    // .org/content/groups/public/org/robolectric/android-all/
    downloadFrom 'https://oss.sonatype.org/content/groups/public/org/robolectric/android-all/4' +
            '.3_r2-robolectric-0/android-all-4.3_r2-robolectric-0.jar'

    keep "android.text.TextUtils"
}

dependencies {
    def appDependencies = rootProject.ext.appDependencies
    def appTestDependencies = rootProject.ext.appTestDependencies

    compile project(':model')
    compile (appDependencies.fresco) {
        exclude module: 'support-v4'
    }
    compile (appDependencies.iconifyMD) {
        exclude module: 'support-v4'
    }
    compile (appDependencies.once) {
        exclude module: 'support-annotations'
    }
    compile appDependencies.stetho
    compile appDependencies.leakCanary
    apt appDependencies.daggerCompiler
    apt appDependencies.autoBundleApt
    compile appDependencies.recyclerViewV7
    compile appDependencies.cardViewV7
    compile appDependencies.flexLayout
    compile appDependencies.SSIV

    compile(appDependencies.fabricAnswer) {
        transitive = true;
    }

    androidTestCompile project(':testbase')
    androidTestCompile (appTestDependencies.androidJUnitRunner) {
        exclude module: 'support-annotations'
    }
    androidTestCompile (appTestDependencies.mockito) {
        exclude module: 'hamcrest-core'
    }
    androidTestCompile appTestDependencies.dexmaker
    androidTestCompile (appTestDependencies.dexmakerMockito) {
        exclude module: 'hamcrest-core'
        exclude module: 'mockito-core'
    }
    androidTestCompile (appTestDependencies.androidJUnit4Rules) {
        exclude module: 'support-annotations'
        exclude module: 'hamcrest-core'
    }
    androidTestApt (appDependencies.daggerCompiler) {
        exclude module: 'dagger'
    }
}
