apply plugin: 'com.android.library'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'com.ofg.uptodate'
apply from: '../AndroidCodeQualityConfig/quality.gradle'

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.androidBuildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

unMock {
    // URI to download the android-all.jar from. e.g. https://oss.sonatype
    // .org/content/groups/public/org/robolectric/android-all/
    downloadFrom 'https://oss.sonatype.org/content/groups/public/org/robolectric/android-all/4' +
            '.3_r2-robolectric-0/android-all-4.3_r2-robolectric-0.jar'

    keep "android.os.Looper"
    keepStartingWith "android.util."
}

dependencies {
    def commonAndroidDependencies = rootProject.ext.commonAndroidDependencies
    def commonAndroidTestDependencies = rootProject.ext.commonAndroidTestDependencies

    compile project(':common')

    compile commonAndroidDependencies.retrofit
    compile commonAndroidDependencies.eventBus
    provided commonAndroidDependencies.daggerCompiler
    compile commonAndroidDependencies.javaxAnnotation

    compile commonAndroidDependencies.supportAnnotation
    compile commonAndroidDependencies.supportV4
    compile commonAndroidDependencies.threetenabp
    compile commonAndroidDependencies.timber

    compile commonAndroidDependencies.autoParcel
    apt commonAndroidDependencies.autoParcelProcessor

    retrolambdaConfig commonAndroidDependencies.retrolambda

    testCompile commonAndroidTestDependencies.junit
    testCompile commonAndroidTestDependencies.mockito
    testCompile commonAndroidTestDependencies.jsonAssert
}
